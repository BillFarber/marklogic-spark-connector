plugins {
  id "java-library"
  id "jacoco-report-aggregation"
  id "org.sonarqube" version "5.1.0.4882"
}

// See https://docs.gradle.org/current/samples/sample_jvm_multi_project_with_code_coverage_standalone.html .
// This isn't picking up jacoco coverage of all the subprojects. Will figure that out soon.
dependencies {
  jacocoAggregation project(':marklogic-langchain4j')
  jacocoAggregation project(':marklogic-spark-api')
  jacocoAggregation project(':marklogic-spark-langchain4j')
  jacocoAggregation project(':marklogic-spark-connector')
}

sonar {
  properties {
    property "sonar.projectKey", "marklogic-spark"
    property "sonar.host.url", "http://localhost:9000"
  }
}

subprojects {
  apply plugin: "java-library"
  apply plugin: "jacoco"
  apply plugin: "org.sonarqube"

  group = "com.marklogic"
  version "2.5-SNAPSHOT"

  java {
    sourceCompatibility = 11
    targetCompatibility = 11
  }

  repositories {
    mavenCentral()
    mavenLocal()
    maven {
      url "https://bed-artifactory.bedford.progress.com:443/artifactory/ml-maven-snapshots/"
    }
  }

  test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    testLogging {
      events 'started', 'passed', 'skipped', 'failed'
      exceptionFormat 'full'
    }
  }

  // See https://docs.gradle.org/current/userguide/jacoco_plugin.html .
  jacocoTestReport {
    dependsOn test
    reports {
      xml.required = true
    }
  }
}
