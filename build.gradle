plugins {
  id 'java'
  id 'net.saliman.properties' version '1.5.2'
  id 'com.github.johnrengelman.shadow' version '7.1.2'
  id "com.marklogic.ml-gradle" version "4.5.1"
}

group 'org.example'
version '1.0-SNAPSHOT'

java {
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
}

repositories {
  // Temporarily here for resolving Java Client 6.2-SNAPSHOT.
  maven {
    url "https://nexus.marklogic.com/repository/maven-snapshots/"
  }
  mavenLocal()

  mavenCentral()
}

dependencies {
  compileOnly 'org.apache.spark:spark-sql_2.12:3.4.0'
  implementation "com.marklogic:marklogic-client-api:6.2-SNAPSHOT"

  // Makes it possible to use lambdas in Java 8 to implement Spark's Function1 and Function2 interfaces
  // See https://github.com/scala/scala-java8-compat for more information
  implementation("org.scala-lang.modules:scala-java8-compat_2.12:1.0.2") {
    // Prefer the Scala libraries used within the user's Spark runtime.
    exclude module: "scala-library"
  }

  testImplementation 'org.apache.spark:spark-sql_2.12:3.4.0'
  testImplementation 'com.marklogic:marklogic-junit5:1.3.0'
  testImplementation "ch.qos.logback:logback-classic:1.3.5"
  testImplementation "org.slf4j:jcl-over-slf4j:1.7.36"
  testImplementation "org.skyscreamer:jsonassert:1.5.1"
  testImplementation "com.marklogic:ml-app-deployer:4.5.1"
}

test {
  useJUnitPlatform()
}

shadowJar {
  // Spark uses an older version of OkHttp; see
  // https://stackoverflow.com/questions/61147800/how-to-override-spark-jars-while-running-spark-submit-command-in-cluster-mode
  // for more information on why these are relocated.
  relocate "okhttp3", "com.marklogic.okhttp3"
  relocate "okio", "com.marklogic.okio"
}
